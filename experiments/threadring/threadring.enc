#! /usr/bin/env encorec -run

class Agent
  id: int
  next: Agent

  def init(id: int): void{
    this.id = id;
    this.next = null;
  }

  def add(a: Agent): Agent {
    this.next = a;
    this.next;
  }

  def pass(hops: int): void {
    if(hops > 0) then {
      this.next.pass(hops-1); -- get causes a deadlock
      ();
    } else {
      print("Finish: {}\n", this.id);
    }
  }


class Main
  def main(): Fut void {
    let 
      n_agents = 2
      hops = 50*1000*1000
      first_agent = new Agent(1)
      prev_agent = new Agent(2)
    in {
      --
      -- Setup the agents
      --
      get first_agent.add(prev_agent);
      while(n_agents+1 <= 503){
        -- do not remove get.
        prev_agent = get prev_agent.add( new Agent(n_agents+1) );	
	n_agents = n_agents +1;
      };
      get prev_agent.add(first_agent);
      
      --
      -- Run threadring
      --
      first_agent.pass(hops);
    }
  }
